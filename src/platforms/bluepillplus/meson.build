# This file is part of the Black Magic Debug project.
#
# Copyright (C) 2024 1BitSquared <info@1bitsquared.com>
# Written by ALTracer <11005378+ALTracer@users.noreply.github.com>
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice, this
#    list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# 3. Neither the name of the copyright holder nor the names of its
#    contributors may be used to endorse or promote products derived from
#    this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
# SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
# CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
# OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

probe_bluepillplus_includes = include_directories('.')

probe_bluepillplus_sources = files('platform.c')

probe_bluepillplus_dfu_sources = files('usbdfu.c')

probe_bluepillplus_args = [
	'-DDFU_SERIAL_LENGTH=9',
	'-DBLACKMAGIC',
]

trace_protocol = get_option('trace_protocol')
probe_bluepillplus_args += [f'-DSWO_ENCODING=@trace_protocol@']
probe_bluepillplus_dependencies = [platform_stm32_swo]
if trace_protocol in ['1', '3']
	probe_bluepillplus_dependencies += platform_stm32_swo_manchester
endif
if trace_protocol in ['2', '3']
	probe_bluepillplus_dependencies += platform_stm32_swo_uart
endif

probe_bluepillplus_common_link_args = [
	'-L@0@'.format(meson.current_source_dir()),
	'-T@0@'.format('stm32f103cb.ld'),
]

probe_bluepillplus_link_args = [
	'-Wl,-Ttext=0x8002000',
]

probe_host = declare_dependency(
	include_directories: probe_bluepillplus_includes,
	sources: probe_bluepillplus_sources,
	compile_args: probe_bluepillplus_args,
	link_args: probe_bluepillplus_common_link_args + probe_bluepillplus_link_args,
	dependencies: [platform_common, platform_stm32f1, probe_bluepillplus_dependencies],
)

probe_bootloader = declare_dependency(
	include_directories: [platform_common_includes, probe_bluepillplus_includes],
	sources: probe_bluepillplus_dfu_sources,
	compile_args: probe_bluepillplus_args,
	link_args: probe_bluepillplus_common_link_args,
	dependencies: platform_stm32f1_dfu,
)

summary(
	{
		'Name': 'Black Magic Probe (BluePill-Plus)',
		'Platform': 'STM32F1',
		'Bootloader': 'Black Magic Debug Bootloader',
		'Load Address': '0x8002000',
	},
	section: 'Probe',
)
